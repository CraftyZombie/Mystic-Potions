import net.fabricmc.loom.api.ModSettings

plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.modrinth.minotaur" version "2.+"
    id 'org.quiltmc.loom' version '1.+'
}

repositories {
    maven { url "https://maven.quiltmc.org/repository/release/" }
}

architectury {
    platformSetupLoomIde()
    loader("quilt")
}


loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
    mods {
        // This should match your mod id.
        "mystic_potions" {
            // Tell Loom about each source set used by your mod here. This ensures that your mod's classes are properly transformed by Loader.
            sourceSet("main")
            // If you shade (directly include classes, not JiJ) a dependency into your mod, include it here using one of these methods:
            // dependency("com.example.shadowedmod:1.2.3")
            configuration("transformProductionQuilt")
            //configuration("exampleShadedConfigurationName")


        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentQuilt.extendsFrom common
}

dependencies {
    repositories {
        maven {
            url "https://cursemaven.com"
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = "Modrinth"
                    url = "https://api.modrinth.com/maven"
                }
            }
            filter {
                includeGroup "maven.modrinth"
            }
        }
    }
    //include project(":common")
    modImplementation "org.quiltmc:quilt-loader:${rootProject.quilt_loader_version}"
    modApi "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${rootProject.quilt_fabric_api_version}"
    // Remove the next few lines if you don't want to depend on the API
    modApi("dev.architectury:architectury-fabric:${rootProject.architectury_version}") {
        exclude group: "net.fabricmc"
        exclude group: "net.fabricmc.fabric-api"
    }

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionQuilt")) { transitive false }
    common(project(path: ":fabric-like", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":fabric-like", configuration: "transformProductionQuilt")) { transitive false }
}

processResources {
    inputs.property "group", rootProject.maven_group
    inputs.property "version", project.version
    inputs.property "architecturyVersion", architectury_version
    inputs.property "minecraftVersion", minecraft_version
    inputs.property "quiltLoaderVersion", quilt_loader_version

    filesMatching("quilt.mod.json") {
        expand "group": maven_group,
                "version": version,
                "architecturyVersion": architectury_version,
                "minecraftVersion": minecraft_version,
                "quiltLoaderVersion": quilt_loader_version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    //archiveClassifier "${minecraft_version}-${quilt_suffix}"
}

jar {
    //archiveClassifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenQuilt(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    versionName = "${project.archives_base_name}-${project.mod_version}-${project.minecraft_version}-${project.quilt_suffix}"
    projectId = project.modrinth_id
    versionNumber = project.mod_version
    versionType = project.quilt_release_type
    uploadFile = remapJar
    gameVersions = [project.minecraft_version]
    loaders = ["quilt"]
    changelog = project.changelog
    dependencies {
        required.version project.fabric_modrinth_dependancies
    }
    debugMode = project.quilt_debug.toBoolean()
}
tasks.getByName('curseforge').dependsOn(':build', ':quilt:remapJar')
curseforge {
    apiKey = System.getenv("CURSEFORGE_API")
    project {
        id = project.curseforge_id
        changelog = project.changelog
        releaseType = project.quilt_release_type
        addGameVersion 'Quilt'
        addGameVersion project.minecraft_version
        relations {
            requiredDependency project.curseforge_dependency
        }
        mainArtifact(remapJar) {
            displayName = "${project.archives_base_name}-${project.mod_version}-${project.minecraft_version}-${project.quilt_suffix}"
        }

    }
    options {
        debug = project.quilt_debug.toBoolean()
    }
}