plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
    id "io.github.pacifistmc.forgix" version "1.2.+"
    id "com.modrinth.minotaur" version "2.+"
    id("com.matthewprenger.cursegradle") version "1.4.+"
}

architectury {
    minecraft = rootProject.minecraft_version
}

forgix {
    group = project.maven_group
    mergedJarName = "${rootProject.archives_base_name}-" + "${project.mod_version}-" + "${rootProject.minecraft_version}-Multi.jar"
    outputDir = "build/libs/merged"
    forge {
        projectName = "forge"
        jarLocation =  "build/libs/${rootProject.archives_base_name}-" + "${project.mod_version}-" + "${rootProject.minecraft_version}-Forge.jar"
    }
    fabric {
        projectName = "fabric"
        jarLocation =  "build/libs/${rootProject.archives_base_name}-" + "${project.mod_version}-" + "${rootProject.minecraft_version}-Fabric.jar"
    }
    quilt {
        projectName = 'quilt'
        jarLocation =  "build/libs/${rootProject.archives_base_name}-" + "${project.mod_version}-" + "${rootProject.minecraft_version}-Quilt.jar"
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()
        //mappings "net.fabricmc:yarn:1.19.2+build.3:v2"
        //mappings loom.layered() {
        //    officialMojangMappings()
        //    parchment("org.parchmentmc.data:parchment-1.19.2:2022.08.10@zip")
        //}
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"
    apply plugin: "com.matthewprenger.cursegradle"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven {
            url = "https://maven.parchmentmc.org"
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
tasks.getByName('modrinth').dependsOn(':clean', ':build', ':mergeJars', ':quilt:modrinth', ':fabric:modrinth', ':forge:modrinth')

modrinth{
    token = System.getenv("MODRINTH_TOKEN")
    versionName = "${project.archives_base_name}-" + "${project.mod_version}-" + "${project.minecraft_version}-Multi"
    projectId = "5UdtzIKo"
    versionNumber = project.mod_version
    versionType = "release"
    uploadFile = forgix.getOutputDir() + "/" + forgix.getMergedJarName()
    gameVersions = [project.minecraft_version]
    loaders = ["forge", "fabric", "quilt"]
    changelog = project.changelog
    dependencies{
        optional.version "cH9PCvfY"
        optional.version "xjWpId6m"
    }
    debugMode = false
}

//tasks.curseforge.dependsOn(':build', ':mergeJars')
curseforge{
    apiKey = System.getenv("CURSEFORGE_API")
    project {
        id = '682855'
        changelog = project.changelog
        releaseType = 'release'
        addGameVersion 'Forge'
        addGameVersion 'Fabric'
        addGameVersion 'Quilt'
        addGameVersion project.minecraft_version
        relations {
            requiredDependency 'architectury-api'
        }
        mainArtifact(forgix.getOutputDir() + "/" + forgix.getMergedJarName()){
            displayName = "${project.archives_base_name}-" + "${project.mod_version}-" + "${project.minecraft_version}-Multi"
        }

    }
    options{
        debug = false
    }
}
